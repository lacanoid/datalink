#!/usr/bin/perl
# pg_datalinker - SQL/MED datalinker daemon
# version 0.25 by lacanoid@ljudmila.org

our $version="0.25.1231";
our $lockid=0x41444154494c4b4e; # "DATALINK" as ASCII

use strict;
use DBI;
use JSON;
use IPC::Run 'run';
use Getopt::Std;
use Time::HiRes qw{usleep};
our $json = JSON->new->allow_nonref;

BEGIN { $ENV{LC_ALL} = "C"; }
#---------------------------------------

our $opt_v = 0;  # verbosity
our $opt_D = 1;  # really delete linked files with ON UNLINK DELETE
our $opt_O = 0;  # for read_access='DB' set linked file owner to relowner if possible
our $opt_R = 1;  # restore old files from backups
our $opt_P = 1;  # process dl_new_files (purge temporary and files from aborted transactions)
our $opt_L = 1;  # use advisory locks (support for datalink.commit())
our $opt_C = 0;  # only check configuration
our $opt_I = 1;  # use internal functions (faster)
our $opt_n = 20; # number of iterations in fast inner loop
our $opt_r = 10; # when fatal error wait this long then restarts
our $opt_e = 1;  # echo commands
our $opt_l;      # logfile
our $macOS = 0;  # running on macOS

getopt('l');

#---------------------------------------

our %config = (
    appname => 'pg_datalinker',
    dsn => 'dbi:Pg:service=pg_datalink',
    group => 'www-data',
    owner => 'postgres',
    db_mode => '0440',
    prefix_file => "/etc/postgresql-common/pg_datalinker.prefix",
    prefix => ["/var/www/datalink/"]
);

my ($operation)=@ARGV;
if($operation eq 'init') { config_init(); exit; }
config_load();
if($operation eq 'list') { config_list(); exit; }

if($operation eq 'run') { main(); exit; }

print STDERR qq{Usage: $0 <command> [arguments...]
Commands:
    list   - list volumes
    run    - run datalinker interactively
};

exit 1;


#---------------------------------------

our @wget = qw{wget --verbose --adjust-extension --span-hosts --convert-links --backup-converted --page-requisites};
our $dbh;
our $dbmeta = {};
our %q;
our @err;

$|=1;

#---------------------------------------

sub main() {
    if($opt_l) {
        # redirect output to file
        open STDOUT, ">>", $opt_l or die "$0: open: $!";
        open STDERR, ">&STDOUT"   or die "$0: dup: $!";
    }
    my $ready;

    #---------------------------------------
    # open database
    while(!($ready>0)) {
        log_debug('CONNECT');
        $dbh = DBI->connect($config{dsn});
        if($dbh) { $ready=1; } 
        else {
            if($ready==0) {
                log_err("Database not available\nHINT:run dlfs bind");
            }
            sleep $opt_r;
            $ready=-1;
        }
    }
    #---------------------------------------
    # check if datalink extension is installed
    $ready = 0;
    while(!($ready>0)) {
        log_debug('GET READY');
        $dbmeta = $dbh->selectrow_hashref(qq{
         select current_database() as datname,
                (select setting from pg_settings where name='port') as port, 
         	    (select extversion from pg_extension where extname='datalink') as extversion,
                current_timestamp as start,
                (select count(*) from pg_stat_activity
                 where application_name='$config{appname}') as nl,
                version()
        });
        if($dbmeta->{extversion}) { $ready=1; } 
        else {
            if($ready==0) {
                log_err("Datalink extension is not installed!\nHINT:create extension datalink");
            }
            sleep $opt_r;
            $ready=-1;
        }
    }

    if($dbmeta->{nl}>0) {
        log_fatal("There seems to be a datalinker already running!");
    }

    log_msg("VERSION",$version);
    log_msg("START",$dbmeta);

    #---------------------------------------
    # setup connection

    $dbh->do("set application_name='".$config{appname}."'");
    $dbh->do("set search_path=datalink");
    $dbh->do('listen "datalink.linker_jobs"');
    $dbh->do("update datalink.dl_status set pid=default,version='$version',cpid='$$',ctime=now()");

    #---------------------------------------
    # prepare queries

    %q = (
        'jobs_get'=>$dbh->prepare(q{
          select lf.*,lco.*,c.relowner::regrole::text as relowner 
            from dl_linked_files lf join link_control_options lco using (lco)
            left join pg_class c on c.oid=lf.attrelid
           where state in ('LINK','UNLINK')
           order by txid limit 1
             for update of lf
        }),
        'job_set'=>$dbh->prepare(q{
          update dl_linked_files set state = $2, info  = $3, err   = $4
           where path  = $1
        }),
        'job_delete'=>$dbh->prepare(q{ delete from dl_linked_files where path = $1 }),

        'mtime'=>$dbh->prepare(q{
          update dl_status set mtime=now(), links = links + $1, unlinks = unlinks + $2
        }),
        'err++'=>$dbh->prepare(q{ update dl_status set errs = errs + 1 }),

        'purge_get'=>$dbh->prepare(q{
          select pg_xact_status(txid) as state, op, path 
            from dl_new_files af
           where op in ('t','w') 
             and pg_xact_status(txid) is distinct from 'in progress'
           order by txid for update of af
        }),
        'purge_delete'=>$dbh->prepare(q{ delete from dl_new_files where path = $1 })
    );

    #---------------------------------------

    if($opt_L) { $dbh->do("select pg_advisory_lock($lockid)"); }
    while(1) {
        log_debug('READY');
        config_observe();
        my $sleepy = 0;
	    my $cl = 0; my $cu = 0; # link/unlink counters
        while(!$sleepy) {
            my $rv = queryOrDie('jobs_get');
            $sleepy = $rv<=0;
            if(!$sleepy) {
                while(my $data = $q{jobs_get}->fetchrow_hashref) {
                    if(!$data->{path}) { 
                        log_err("INPUT","No path in ",$data); next; 
                    }
                    if(!$data->{token}) { 
                        log_err("INPUT","No token in ",$data); next; 
                    }

                    my $path = $data->{path}; 
                    my $path_backup = path_backup($data->{path},$data->{token});

                    if(!path_valid_prefix($path)) { 
                        log_err("PREFIX",sprintf("Bad path prefix in '$path'")); 
                        queryOrDie('job_set',$path,'ERROR',undef,'[{"code":"PREFIX"}]');
                        queryOrDie('err++');
                        next; 
                    }
                    @err=();
                    my $info;
                    if($data->{info}) { $info=$data->{info}=$json->decode($data->{info}); }
                    log_debug("DATA",$data);
                    my $ts=iso_timestamp();
                    my $u=$data->{relowner};
                    if($data->{state} eq 'LINK') {
                        if($info) {
                            log_msg("RE-LINK:$ts:$u:".$data->{lco},$path_backup);
			                $info->{del}=0;
                            file_unlink($path,$data); $cu++;
                        } else {
                            log_msg("LINK:$ts:$u:".$data->{lco},$path_backup);
                        }
                        my $r=file_link($path,$data); $cl++;
                        my $err=(@err)?to_json(\@err):undef;
                        queryOrDie('job_set',$path,$err?'ERROR':'LINKED',$json->encode($r),$err);
                        if($err) { queryOrDie('err++'); }
                        next;
                    }
                    elsif($data->{state} eq 'UNLINK') {
                        log_msg("UNLINK:$ts:".$data->{lco},$path_backup);
                        file_unlink($path,$data); $cu++;
                        queryOrDie('job_delete',$path);
                        next;
                    }
                    else {
                        log_err("STATE","Unknown state in ",$data);
                        next;
                    } 
                } # while fetchrow jobs_get
                queryOrDie('mtime',$cl,$cu);
            } # if not sleepy
        } # while not sleepy

        if($opt_P) { purge_files(); }

        if($opt_L) { # this is to make datalink.commit() work
            $dbh->do("select pg_advisory_unlock($lockid)");
            $dbh->do("select pg_advisory_lock($lockid)");  }

        if($sleepy) {
            log_debug('IDLE');
            for(my $i=0;$i<$opt_n;$i++) {
                while(my $not = $dbh->pg_notifies) {
                    log_debug('NOTIFY',$not);
                    $sleepy = 0;
                }
                if(!$sleepy) { next; }
                else {
                    usleep(1000000 * 5.0/$opt_n);
                } 
            }
        }
        $dbh->do("update datalink.dl_status set atime=now()");
    } # forever loop

    if($opt_L) { $dbh->do("select pg_advisory_unlock($lockid)"); }

    log_msg('END');
}

########################################
# process entries in dl_new_files
# 1. delete files created in aborted transactions

sub purge_files {
    my $rv = queryOrDie('purge_get');
    while(my $data = $q{purge_get}->fetchrow_hashref) {
        my $path = $data->{path}; 
        if(path_valid_prefix($path)) {
            my $state = $data->{state};
            my $op = $data->{op};
            if($state eq 'in progress') { next; } 
            my $stat = file_stat($path);
            if($stat->{inode}) {
                if($stat->{owner} eq $config{owner}) {
                    log_msg('PURGE',"$op,$state,$path");
                    if($state eq 'aborted' || ($state eq 'committed' && $op eq 't')) {
			            file_delete($path);
		            }
                } else { 
                    log_err("BAD OWNER","File '$path' is not owned by '$config{owner}'.");
                }
            } else { 
                # log_err("FILE DOES NOT EXISTS","File '$path' does not exist.");
            }
        } 
        queryOrDie('purge_delete',$path);
    }
}

########################################

sub queryOrDie {
    my $query=shift @_;
    my @args=@_;
    my $rv=$q{$query}->execute(@args);
    if(!defined($rv)) {
        log_fatal("Can't execute '$query' query.\nHINT:Make sure datalink extension is installed.");
    }
    return $rv;
}

########################################
# configuration

# load configuration
sub config_load {
    if($config{prefix_file}) {
        if(-r $config{prefix_file}) {
            my @l = file_get_lines($config{prefix_file});
            $config{prefix}=\@l;
        } else {
            log_fatal("No prefix file in $config{prefix_file}\nHINT:Did you run pg_datalinker init?");
        }
    } else { log_fatal("config.prefix_file not set"); }
    config_check();
}
# check if configuration is valid
sub config_check {
    my $p1 = [];
    for my $prefix (@{$config{prefix}}) {
        if($prefix) { push @$p1,$prefix; }
    }
    $config{prefix}=$p1;
}
# config list [refixes]
sub config_list {
    for my $prefix (@{$config{prefix}}) {
        print "$prefix\n";
    }
}
# config list [refixes]
sub config_observe {
    if($config{prefix_file}) {
        if(file_observe($config{prefix_file})) { 
            log_msg('CONFIGURE',$config{prefix_file});
            config_load(); 
        }
    } else { log_fatal("config.prefix_file not set"); }
}
# create inital system configuration if there is none
sub config_init {
    if(-f $config{prefix_file}) {
        log_err("FILE EXISTS","Config file ".$config{prefix_file}." exists.");
    }
}

########################################
# path management

sub path_backup {
    my ($path,$token)=@_;
    if($token) { return $path.'#'.$token; }
    else { return $path; }
}

sub path_valid_prefix {
    my ($path)=@_;
    my $prefix;
    for $prefix (@{$config{prefix}}) {
       if(rindex($path,$prefix,0)==0) { return 1 }
    }
    return 0
}

sub path_valid {
    my ($path)=@_;
    # illegal chars % and *
    if($path=~m|[%*]|) { return 0; } 
    # not absolute
    if(!(rindex($path,'/')==0)) { return 0; }
    # parent
    if(rindex($path,'/../')>=0) { return 0; }
    return path_valid_prefix($path);
}

########################################
# file management

sub file_stat {
    my ($path,$opt)=@_;
    my (@s) = lstat($path);
    unless(-e $path) { return undef; }
    my $typs="?pc?d?b?-?l?s???"; # file types as shown by ls(1)
    return {
        'dev'=>$s[0],'inode'=>$s[1],'nlink'=>$s[3],
        'mode'=>($s[2] & 07777),
        'typ'=>substr($typs,(($s[2] & 0170000)>>12),1),
        'uid'=>$s[4],'gid'=>$s[5],
        'rdev'=>$s[6],'size'=>$s[7],
        'atime'=>$s[8],'mtime'=>$s[9],'ctime'=>$s[10],
        'blksize'=>$s[11],'blocks'=>$s[12],
        'owner'=>getpwuid($s[4]),'group'=>getgrgid($s[5])
    };
}

# link file to sql environment
sub file_link {
    my ($path,$opt)=@_;
    $path =~ s|/+$||;
    my $path1 = $path; # backup path
    my $have = 0;      # have base file
    my $s = file_stat($path,$opt);
    if($s->{inode}) { $have=1; }
    else {
        $path1 = path_backup($path,$opt->{token});
        $s = file_stat($path1,$opt);
    }
    if(!$s->{inode}) {
        log_err("NO STAT",$path);
        return undef;
    }

#    if($opt->{read_access} eq 'DB') {
#        file_read_access_db($path1,$opt);
#    }

    if(file_has_backup($path,$opt)) { # has backup
        if(!$have || $opt->{recovery} eq 'YES' || 
           $opt->{write_access} eq 'ADMIN' || $opt->{write_access} eq 'TOKEN') {
            file_backup_restore($path,$opt);
        }
        if($opt->{read_access} eq 'DB') { file_read_access_db($path,$opt); }
    } else { # no backup
        if($opt->{read_access} eq 'DB') { file_read_access_db($path,$opt); }
        if($opt->{recovery} eq 'YES') { file_backup($path,$opt); }
    } 
    
    my $r = { own => $s->{uid}.":".$s->{gid}, 
              mod => sprintf("%#o",$s->{mode} & 07777),
              lco => $opt->{lco},
              token => $opt->{token},
              del => ($opt->{on_unlink} eq 'DELETE'),
              addr => '{'.$s->{dev}.','.$s->{inode}.'}'
    };

    if($opt->{write_access} ne 'FS') {
        file_block($path);
    }

    return $r;
}
# unlink file from sql environment
sub file_unlink {
    my ($path,$opt)=@_;
    if($opt->{write_access} ne 'FS') {
        file_unblock($path,$opt);
    }
    if($opt->{on_unlink} eq 'DELETE') {
#    if($opt->{info} && $opt->{info}{del}) {
        file_unlink_delete($path,$opt);
    }
    if($opt->{read_access} eq 'DB') {
	    if(-f $path || -d $path) { file_restore($path,$opt); }
    }
}
# set file permissions for read_access db
sub file_read_access_db {
    my ($path,$opt)=@_;
    log_debug('CHOWNMOD',$opt);
    if(defined($config{owner})) {
        if(defined($config{group})) { sys("chown",$config{owner}.':'.$config{group},$path); } 
        else { sys("chown",$config{owner},$path); }
    }
    if($opt_O && $opt->{relowner}) {
        my ($name, $pass, $uid, $gid, $quota, $comment, $gcos, $dir, $shell, $expire) = getpwnam($opt->{relowner});
        sys("chown",$name,$path);
    }
    if(defined($config{db_mode})) { file_chmod($path,$config{db_mode}); }
}

sub file_restore {
    my ($path,$opt)=@_;
    my $info = $opt->{info};
    log_debug('RESTOR',$opt);
    if($info) {
        if(defined($info->{own})) {
            sys("chown",$info->{own},$path);
        }
        if(defined($info->{mod})) { file_chmod($path,$info->{mod}); }
    }
}

sub file_chown {
    my ($path,$own,$grp)=@_;
}

sub file_chmod {
    my ($path,$mod)=@_;
    if($opt_I) { 
        log_msg('CHMOD',$mod);
        chmod(oct($mod),$path); return; 
    }
    sys("chmod",$mod,$path);
}

sub file_delete {
    my ($path,$opt)=@_;
    log_debug('FILE DELETE',$opt);
    if(!$opt_D) {
	    log_msg('NO DELETE',"Delete disabled: '$path'. HINT:use -D option");
	    return;
    }
    if($opt) { log_msg('DELETE',$path); }
    else {
        my $s = file_stat($path);
        if(!($s->{owner} eq $config{owner})) {
        	log_err("BAD OWNER","File '$path' is not owned by '$config{owner}'.");
    	    return;
        }
    }
    if($opt_I) { unlink($path); }
    else { sys('rm',$path); }
}

sub file_unlink_delete {
    my ($path,$opt)=@_;
    my $info = $opt->{info};
    if(($info && $info->{del}) || !$info) {
        if(!($opt->{on_unlink} eq 'DELETE')) {
            log_err('NO DELETE',"Not ON UNLINK DELETE");
            return;
        }
#        if(defined($info->{own})) { file_delete($path,$opt); }
        file_delete($path,$opt);
    }
}
# does file have a backup copy
sub file_has_backup {
    my ($path,$opt)=@_;
    my $path_backup = path_backup($path,$opt->{token});
    return -f $path_backup;
}
# backup a file
sub file_backup {
    my ($path,$opt)=@_;
    if($opt->{token}) {
        my $path_backup = path_backup($path,$opt->{token});
        log_msg('BACKUP',$path_backup);
        if(-f $path_backup) {
            log_err("COPY EXISTS","File exists:$path_backup");
        } else {
            sys("cp","-a",$path,$path_backup);
        }
    }
}
# revert a file to previous version from backup
sub file_backup_restore {
    my ($path,$opt)=@_;
    my $move = !($opt->{recovery} eq 'YES');
     if($opt->{token}) {
        my $path_backup = path_backup($path,$opt->{token});
        if(-f $path && !$opt_R) {
            log_err("NO RESTORE","Restore disabled: HINT:use -R option");
            return;
        }
        if(-f $path_backup) {
            if($move) {
                log_msg('REPLACE',$path_backup);
                sys("mv","-f",$path_backup,$path);
            }
            else {
                log_msg('RESTORE',$path_backup);
                sys("ln","-f",$path_backup,$path);
            }
        } else {
            log_err("NO BACKUP","File not found:$path_backup");
        }
     }
}
# block file for writing
sub file_block {
    my ($path,$opt)=@_;
    if($macOS) { sys("chflags","schg",$path); } 
    else { sys("chattr","+i",$path); }
}
# unblock file for writing
sub file_unblock {
    my ($path,$opt)=@_;
    if($macOS) { sys("chflags","noschg",$path); } 
    else { sys("chattr","-i",$path); }
}
# read a file into lines
sub file_get_lines {
    my ($path)=@_;
    if(open(Fg,'<',$path)) {
        chomp(my @lines = <Fg>); close(Fg);
        return @lines;
    } 
    else { log_err("CANT READ","Cannot read file:$path"); }
}
# watch file for changes
our $file_stat = {};
sub file_observe {
    my ($path)=@_;
    my $stat=file_stat($path);
    my $old_mtime = $file_stat->{$path}->{mtime};
    my $new_mtime = $stat->{mtime};
    log_debug('OBSERVE',"$old_mtime,$new_mtime");
    $file_stat->{$path}=$stat;
    if(int($new_mtime)>int($old_mtime)) { return 1; }
    return 0;
}
########################################
# run sys commands

our $syserr;
our $sysex;

sub sys {
    my @args = @_;
    if($opt_e) { log_msg("SYS",[@args]); }
    $sysex = undef; $syserr=undef;
    system(@args);

    if ($? == -1) {
        $syserr = "failed to execute: $!";
    }
    elsif ($? & 127) {
        $syserr = sprintf "died sig %d%s",
            ($? & 127),  ($? & 128) ? ', core' : '';
    }
    else {
        my $ex = $? >> 8;
        if($ex > 0) {
            $syserr = sprintf "exit %d", $ex;
        }
        $sysex = $ex;
    }
    if($?) { log_err('SYSERR',"@args failed: $?"."; ".$syserr); }
}

########################################
# run wget to arhive webpages (not used yet)

sub wget {
    my ($u)=@_;
    my @c = @wget;
    push @c,$u;

    my $start_time = [Time::HiRes::gettimeofday()];

    chdir($config{url_archive});
    run [ @c ], '2>', \my $res;

    my %rc;
    while($res=~s/response\.\.\.\s*(\d+)//) {
    $rc{$1}++;
    }

    my $diff = Time::HiRes::tv_interval($start_time);

    return {'t'=>$diff, 'rc'=>\%rc };
}

########################################
# logging

sub log_debug {
    my ($txt,$data)=@_;
    if($opt_v) {
        print "$txt:".(ref($data)?$json->pretty->encode($data):$data)."\n";
    }
}

sub log_msg {
    my ($txt,$data)=@_;
    print "$txt:".(ref($data)?$json->encode($data):$data)."\n";
}

sub log_err {
    my ($txt,$data)=@_;
    push @err,[$txt,$data];
    if($opt_e) {
        print STDERR "ERROR:$txt:".(ref($data)?$json->pretty->encode($data):$data)."\n";
    }
}

sub log_fatal {
    my ($txt,$data)=@_;
    print STDERR "FATAL:$txt\n";
    if ($DBI::err) {
        print STDERR "DBI-ERROR:".$DBI::err.":".$DBI::errstr."\n";
    }
    if($data) {
        print (ref($data)?$json->encode($data):$data)."\n";
    }
    if($opt_r) {
        print STDERR "RESTART:\n";
        sleep $opt_r;
        exec($^X, $0, @ARGV); # restart
    }
    die;
}

########################################

sub iso_timestamp {
  my ($time)=@_;
  my ($y,$m,$d,$hh24,$mi,$ss);
  if(defined($time)) {
    ($y,$m,$d,$hh24,$mi,$ss)=(gmtime($time))[5,4,3,2,1,0];
  } else {
    ($y,$m,$d,$hh24,$mi,$ss)=(gmtime)[5,4,3,2,1,0];
  }
  sprintf("%04d-%02d-%02dT%02d:%02d:%02dZ",1900+$y,$m+1,$d,$hh24,$mi,$ss);
}
